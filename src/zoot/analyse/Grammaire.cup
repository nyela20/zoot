package zoot.analyse ;

import java.util.*;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.arbre.instructions.* ;
import zoot.exceptions.AnalyseSyntaxiqueException;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    DEBUT, FIN, POINTVIRGULE, ECRIRE, VARIABLES, EGAL;
terminal String    CSTENTIERE, IDF, CSTBOOLEENNE, ENTIER, BOOLEEN ;
   
/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST ;
non terminal Instruction INST ;
non terminal Expression EXP ;
non terminal String TYPE, DCLR;
non terminal ArrayList<String> LISTDCLR;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     DEBUT  LINST:li  FIN
                {: RESULT = li ; :}
                |
                VARIABLES LISTDCLR:ld DEBUT LINST:li  FIN
                {:
                RESULT = li;
                :}
                ;

//Liste d'instructions
LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE //Ecriture
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
                |
                //Affection
                //a = 5;
                //b = vrai;
                //a = c;
                IDF:idf EGAL EXP:e POINTVIRGULE
                {: RESULT = new Affectation(idf,e, eleft +1); :}
                ;
    
EXP    ::=     CSTENTIERE:c
                   {:  RESULT = new ConstanteEntiere(c, cleft + 1);:}
               |
               CSTBOOLEENNE:c
               {: RESULT = new ConstanteBooleenne(c, cleft + 1);:}
               |
               IDF:i
               {: RESULT = new Identifiant(i, ileft +1); :}
               ;

TYPE    ::=     ENTIER
                {: RESULT = "entier"; :}
                |
                BOOLEEN
                {: RESULT = "booleen"; :}
                ;

DCLR    ::=     TYPE:t IDF:idf POINTVIRGULE //Exemples: entier 1; booleen vrai;
                {:
                  TDS.getInstance().ajouter(new EntreeVariable(idf), new SymboleVariable(t, 4));
                  RESULT = idf;
                :}
            ;

//Une liste de decalrations
LISTDCLR    ::=     LISTDCLR:ld DCLR:d
                    {:ld.add(d);
                     RESULT = ld;
                    :}
                    |
                    DCLR:d
                    {:
                        ArrayList<String> ld = new ArrayList<>();
                        ld.add(d);
                        RESULT = ld;
                    :}
                    ;